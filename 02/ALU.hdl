// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    // zx
    Mux16 (a=x, b=false, sel=zx, out=zxout);
    // nx
    Not16 (in=zxout, out=notx);
    Mux16 (a=zxout, b=notx, sel=nx, out=nxout);
    // zy
    Mux16 (a=y, b=false, sel=zy, out=zyout);
    // ny
    Not16 (in=zyout, out=noty);
    Mux16 (a=zyout, b=noty, sel=ny, out=nyout);
    // f
    Add16 (a=nxout, b=nyout, out=xplusy);
    And16 (a=nxout, b=nyout, out=xandy);
    Mux16 (a=xandy, b=xplusy, sel=f, out=fout);
    // no
    Not16 (in=fout, out=notf);
    Mux16 (a=fout, b=notf, sel=no, out[0..7]=tempoutzrfst, out[8..15]=tempoutzrsec, out[15]=tempoutng, out=out);

    //zr
    Or8Way (in=tempoutzrfst, out=outfst);
    Or8Way (in=tempoutzrsec, out=outsec);
    Or (a=outfst, b=outsec, out=notzr);
    Not (in=notzr, out=zr);

    //ng
    FullAdder (a=tempoutng, b=true, sum=overflow, carry=ng);
}